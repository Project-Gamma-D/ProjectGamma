plugins {
    id 'java'
    id 'application'
}

group 'com.projectgamma'
version '1.0-dev'

repositories {
    mavenCentral()
    maven {
        url 'https://libraries.minecraft.net'
    }
}

dependencies {
    implementation 'net.dv8tion:JDA:5.0.0-alpha.6'
    implementation 'org.slf4j:slf4j-simple:1.7.36'
    implementation 'com.mojang:brigadier:1.0.18'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

application {
    mainClass = 'com.projectgamma.ProjectGamma.Main'
}

// This will optionally load the bot token from a .token file in the project root and use it to run the program
run {
    try {
        Scanner scanner = new Scanner(layout.projectDirectory.file(".token").getAsFile())
        args scanner.nextLine()
    } catch (Exception ignored) {
        logger.warn('The file .token is missing. This file is needed to authenticate with the Discord API when testing with gradle :run')
    }
}

test {
    useJUnitPlatform()
}