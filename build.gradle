plugins {
    id 'java'
    id 'application'
}

group 'com.projectgamma'
version project.app_version

sourceSets {
    api {
        java {
            srcDirs('src/api')
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://libraries.minecraft.net'
    }
}

dependencies {
    implementation(sourceSets.api.runtimeClasspath)
    implementation "net.dv8tion:JDA:${project.jda_version}"
    implementation "org.slf4j:slf4j-simple:${project.slf4j_simple_version}"
    implementation "com.mojang:brigadier:${project.brigadier_version}"

    apiImplementation "net.dv8tion:JDA:${project.jda_version}"
    apiImplementation "com.mojang:brigadier:${project.brigadier_version}"
}

application {
    mainClass = 'com.projectgamma.ProjectGamma.Main'
}

// This will optionally load the bot token from a .token file in the project root and use it to run the program
run {
    try {
        Scanner scanner = new Scanner(layout.projectDirectory.file('.token').getAsFile())
        args scanner.nextLine()
    } catch (Exception ignored) {
        logger.warn('The file .token is missing. This file is needed to authenticate with the Discord API when testing with gradle :run')
    }
}